plugins {
    id 'com.android.application'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'com.vernu.sms'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
        versionCode 15
        versionName "2.6.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

//        javaCompileOptions {
//            annotationProcessorOptions {
//                arguments["room.schemaLocation"] = "$projectDir/schemas"
//            }
//        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    buildFeatures {
        buildConfig true
    }

    // Read from local.properties
    def localProperties = new Properties()
    def localPropertiesFile = rootProject.file('local.properties')
    if (localPropertiesFile.exists()) {
        localPropertiesFile.withInputStream { stream ->
            localProperties.load(stream)
        }
    }

    flavorDimensions "environment"

    productFlavors {
        dev {
            dimension "environment"
            applicationId "com.vernu.sms"
            versionNameSuffix "-dev"
            buildConfigField "String", "API_BASE_URL", "\"${localProperties.getProperty('dev.api.base.url', 'http://localhost:3001/api/v1/')}\""
            buildConfigField "String", "DASHBOARD_URL", "\"${localProperties.getProperty('dev.dashboard.url', 'http://localhost:3001/dashboard')}\""
            buildConfigField "String", "DOWNLOAD_URL", "\"${localProperties.getProperty('dev.download.url', 'http://localhost:3001/download')}\""
            buildConfigField "String", "ENVIRONMENT", '"development"'
            resValue "string", "app_name", "SMS Gateway (Dev)"
            resValue "string", "dashboard_url", "${localProperties.getProperty('dev.dashboard.url', 'http://localhost:3001/dashboard')}"
            resValue "string", "dashboard_link_text", "Go to ${localProperties.getProperty('dev.dashboard.url', 'http://localhost:3001/dashboard')}"
        }

        prod {
            dimension "environment"
            applicationId "com.vernu.sms"
            buildConfigField "String", "API_BASE_URL", "\"${localProperties.getProperty('prod.api.base.url', 'http://3.147.76.130:3001/api/v1/')}\""
            buildConfigField "String", "DASHBOARD_URL", "\"${localProperties.getProperty('prod.dashboard.url', 'http://3.147.76.130:3001/dashboard')}\""
            buildConfigField "String", "DOWNLOAD_URL", "\"${localProperties.getProperty('prod.download.url', 'http://3.147.76.130:3001/download')}\""
            buildConfigField "String", "ENVIRONMENT", '"production"'
            resValue "string", "app_name", "SMS Gateway"
            resValue "string", "dashboard_url", "${localProperties.getProperty('prod.dashboard.url', 'http://3.147.76.130:3001/dashboard')}"
            resValue "string", "dashboard_link_text", "Go to ${localProperties.getProperty('prod.dashboard.url', 'http://3.147.76.130:3001/dashboard')}"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation platform('com.google.firebase:firebase-bom:29.2.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-messaging-directboot'
    implementation 'com.google.firebase:firebase-crashlytics'

    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'com.journeyapps:zxing-android-embedded:4.1.0'
    
    implementation 'androidx.work:work-runtime:2.7.1'

//    def room_version = "2.4.2"
//    implementation "androidx.room:room-runtime:$room_version"
//    annotationProcessor "androidx.room:room-compiler:$room_version"
}

apply plugin: 'com.google.gms.google-services'